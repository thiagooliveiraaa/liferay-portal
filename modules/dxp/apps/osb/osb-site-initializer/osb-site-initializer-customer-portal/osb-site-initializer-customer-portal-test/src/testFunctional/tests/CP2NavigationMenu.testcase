@component-name = "portal-solutions"
definition {

	property osgi.modules.includes = "osb-site-initializer-customer-portal";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "OSB Site Initializer Customer Portal";

	var testSiteName = "Customer Portal Site";
	var testSiteURL = "customer-portal-site";
	var testAccounts = "Test Account Analytics Cloud,Test Account Commerce,Test Account LXC - SM,Test Account Enterprise Search";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Customer Portal Site") {
			CustomerPortal.addCustomerPortalSite(siteName = ${testSiteName});
		}

		task ("Add test user access to all Accounts") {
			CustomerPortal.assignUserToOrganization();

			CustomerPortal.addUserToOneOrMoreAccounts(accountsList = ${testAccounts});
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		CustomerPortal.customerPortalApisTearDown();

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = ${testSiteName});
		}
	}

	@description = "Automation ID: LPS-143811 | Test Summary: Verify if Analytics Cloud option is showing under Product Activation"
	@priority = 3
	test IsAnalyticsCloudOptionAvailable {
		task ("Given that I enter a project") {
			ApplicationsMenu.gotoSite(key_site = ${testSiteName});

			CustomerPortal.enterOnProject(title = "Test Account Analytics Cloud");
		}

		task ("When I open the Product Activation option in the side menu") {
			CP2NavigationMenu.goToProductActivation();

			CP2NavigationMenu.clickOnProductButton(product = "Analytics Cloud");
		}

		task ("Then I should be able to say that the AC product is an option") {
			AssertElementPresent(
				key_text = "Analytics Cloud",
				locator1 = "Button#ANY");
		}
	}

	@description = "Automation ID: LPS-143801 | Test Summary: Verify if Overview is displayed on Navigation Menu"
	@priority = 4
	test IsOverviewMenuDisplayed {
		task ("Given that I enter a project") {
			ApplicationsMenu.gotoSite(key_site = ${testSiteName});

			CustomerPortal.enterOnProject(title = "Test Account LXC - SM");
		}

		task ("Assert the Overview option in the on side menu") {
			AssertElementPresent(locator1 = "CP2NavigationMenu#OVERVIEW");
		}
	}

	@description = "Automation ID: LPS-143809 | Test Summary: Verify if Team Members is displayed on Navigation Menu"
	@priority = 4
	test IsTeamMembersMenuDisplayed {
		task ("Given that I enter a project") {
			ApplicationsMenu.gotoSite(key_site = ${testSiteName});

			CustomerPortal.enterOnProject(title = "Test Account LXC - SM");
		}

		task ("Assert the Team Members option in the on side menu") {
			CP2NavigationMenu.goToTeamMembers();

			AssertElementPresent(locator1 = "CP2NavigationMenu#TEAM_MEMBERS");
		}
	}

}