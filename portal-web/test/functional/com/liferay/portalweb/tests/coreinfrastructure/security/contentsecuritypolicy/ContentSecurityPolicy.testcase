@component-name = "portal-security"
definition {

	property ci.retries.disabled = "true";
	property custom.properties = "feature.flag.LPS-134060=true";
	property portal.release = "false";
	property portal.upstream = "true";
	property testray.main.component.name = "Content Security Policy";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			PortalSettings.tearDownAuthenticationCP();

			PagesAdmin.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-179552. TC-2. Verify the CSP configuration could be loaded from OSGi configuration file."
	@priority = 5
	test VerifyCSPConfigurationCanBeLoadedFromOSGiFile {
		property test.name.skip.portal.instance = "ContentSecurityPolicy#VerifyCSPConfigurationCanBeLoadedFromOSGiFile";

		task ("Given Add virtual instance and enable CSP at Instance level") {
			HeadlessPortalInstanceAPI.addPortalInstance(
				domain = "www.able.com",
				portalInstanceId = "www.able.com",
				virtualHost = "www.able.com");

			UserBar.signOut();

			User.firstLoginUI(
				password = "test",
				specificURL = "http://www.able.com:8080",
				userEmailAddress = "test@www.able.com");
		}

		task ("When add the OSGI configuration file") {
			ContentSecurityPolicy.deployInstanceConfigFile(
				osgiConfigFileName = "com.liferay.portal.security.content.security.policy.internal.configuration.ContentSecurityPolicyConfiguration.scoped~instance~level.config",
				portalInstanceName = "www.able.com");

			OSGiConfig.waitForOSGiConfig();
		}

		task ("Then: The preconfigured value is present at Instance Settings.") {
			ContentSecurityPolicy.goToContentSecurityPolicyInstanceSettings(baseURL = "http://www.able.com:8080");

			AssertTextPresent(
				locator1 = "ContentSecurityPolicy#CONTENT_SECURITY_POLICY_TEXT",
				value1 = "img-src http://www.able.com");

			AssertTextPresent(
				locator1 = "ContentSecurityPolicy#EXCLUDED_PATHS_TEXT",
				value1 = "/c/portal/layout");
		}
	}

	@description = "This is a use case for LPS-182349. TC-3. Verify the prespecified Content Security Policy settings could be configured at Instance level using Web ID."
	@priority = 4
	test VerifyCSPCouldConfiguredByWebId {
		property test.name.skip.portal.instance = "ContentSecurityPolicy#VerifyCSPCouldConfiguredByWebId";

		task ("Add the OSGI configuration file") {
			ContentSecurityPolicy.deployInstanceConfigFile(osgiConfigFileName = "com.liferay.portal.security.content.security.policy.configuration.ContentSecurityPolicyConfiguration.scoped~custom-image-source-webid.config");

			OSGiConfig.waitForOSGiConfig();
		}

		task ("When: The Instance Administrator opens the Content Security Policy settings at Instance level.") {
			ContentSecurityPolicy.goToContentSecurityPolicyInstanceSettings();
		}

		task ("Then: The preconfigured value is present at Instance Settings.") {
			AssertTextPresent(
				locator1 = "ContentSecurityPolicy#CONTENT_SECURITY_POLICY_TEXT",
				value1 = "img-src domain.liferay.com");
		}

		task ("And: The header is added to any Portal requests.") {
			var baseURL = PropsUtil.get("portal.url");

			var curl = '''${baseURL} -X HEAD -I''';

			var response = JSONCurlUtil.post(${curl});

			TestUtils.assertPartialEquals(
				actual = ${response},
				expected = "Content-Security-Policy: img-src domain.liferay.com");
		}
	}

	@description = "This is a use case for LPS-182349. TC-1, TC-2 and TC-4. Verify the prespecified Content Security Policy settings is present at Instance level and present in the Request headers."
	@priority = 4
	test VerifyCSPCouldConfiguredToBlockImageSource {
		property test.name.skip.portal.instance = "ContentSecurityPolicy#VerifyCSPCouldConfiguredToBlockImageSource";

		task ("Add the OSGI configuration file") {
			ContentSecurityPolicy.deployInstanceConfigFile(osgiConfigFileName = "com.liferay.portal.security.content.security.policy.configuration.ContentSecurityPolicyConfiguration.scoped~custom-image-source.config");

			OSGiConfig.waitForOSGiConfig();
		}

		task ("When: The Instance Administrator opens the Content Security Policy settings at Instance level.") {
			ContentSecurityPolicy.goToContentSecurityPolicyInstanceSettings();
		}

		task ("Then: The preconfigured value is present at Instance Settings.") {
			AssertTextPresent(
				locator1 = "ContentSecurityPolicy#CONTENT_SECURITY_POLICY_TEXT",
				value1 = "img-src domain.example.com");
		}

		task ("And: The header is added to any Portal requests.") {
			var baseURL = PropsUtil.get("portal.url");

			var curl = '''${baseURL} -X HEAD -I''';

			var response = JSONCurlUtil.post(${curl});

			TestUtils.assertPartialEquals(
				actual = ${response},
				expected = "Content-Security-Policy: img-src domain.example.com");
		}
	}

	@description = "This is a use case for LPS-179901. TC-2. Verify the CSP configuration could be loaded from OSGi configuration file."
	@priority = 5
	test VerifyCSPSiteSettingsCanBeLoadedFromOSGiFile {
		property test.name.skip.portal.instance = "ContentSecurityPolicy#VerifyCSPSiteSettingsCanBeLoadedFromOSGiFile";

		task ("Given Add new site and add a new virtual host as SiteURL") {
			HeadlessSite.addSite(siteName = "Site Name");

			JSONLayout.addPublicLayout(
				groupName = "Site Name",
				layoutName = "Test Page");

			Site.openSiteSettingsAdmin(siteURLKey = "site-name");

			Site.addVirtualHostsURLCP(
				pageVirtualHosts = "true",
				pageVirtualHostURL = "www.able.com");
		}

		task ("When add the OSGI configuration file") {
			ContentSecurityPolicy.deploySiteConfigFile(
				groupName = "Site Name",
				osgiConfigFileName = "com.liferay.portal.security.content.security.policy.internal.configuration.ContentSecurityPolicyConfiguration.scoped~site~level.config");

			UserBar.signOut();

			User.firstLoginUI(
				password = "test",
				specificURL = "http://www.able.com:8080",
				userEmailAddress = "test@liferay.com");
		}

		task ("Then: The preconfigured value is present at Site Settings.") {
			ContentSecurityPolicy.goToContentSecurityPolicySiteSettings(
				baseURL = "http://www.able.com:8080",
				siteURLKey = "site-name");

			AssertTextPresent(
				locator1 = "ContentSecurityPolicy#CONTENT_SECURITY_POLICY_TEXT",
				value1 = "img-src http://www.able.com");

			AssertTextPresent(
				locator1 = "ContentSecurityPolicy#EXCLUDED_PATHS_TEXT",
				value1 = "/c/portal/layout");
		}
	}

	@description = "This is a use case for LPS-179552. TC-1. Verify if 'nonce' value is added to all script and link HTML elements of the page."
	@priority = 5
	test VerifyNonceIsAddedWhenCSPIsEnabled {
		property test.name.skip.portal.instance = "ContentSecurityPolicy#VerifyNonceIsAddedWhenCSPIsEnabled";

		task ("Given Add virtual instance and enable CSP at Instance level") {
			HeadlessPortalInstanceAPI.addPortalInstance(
				domain = "www.able.com",
				portalInstanceId = "www.able.com",
				virtualHost = "www.able.com");

			UserBar.signOut();

			User.firstLoginUI(
				password = "test",
				specificURL = "http://www.able.com:8080",
				userEmailAddress = "test@www.able.com");

			ContentSecurityPolicy.goToContentSecurityPolicyInstanceSettings(baseURL = "http://www.able.com:8080");

			ContentSecurityPolicy.configureCSP(
				csp = "img-src http://www.able.com",
				enableCSP = "true");
		}

		task ("When: Navigate to default site page and check HTML page source.") {
			Navigator.openSpecificURL(url = "http://www.able.com:8080");
		}

		task ("Then: Verify 'nonce' value is added to script and link HTML elements of the page.") {
			var pageSource = selenium.getHtmlSource();

			TestUtils.assertPartialEquals(
				actual = ${pageSource},
				expected = "script nonce");

			TestUtils.assertPartialEquals(
				actual = ${pageSource},
				expected = "link nonce");
		}

		task ("When: Add '/web/guest' as Excluded URI Path.") {
			ContentSecurityPolicy.goToContentSecurityPolicyInstanceSettings(baseURL = "http://www.able.com:8080");

			ContentSecurityPolicy.configureCSP(excludedPaths = "/c/portal/layout");
		}

		task ("Then: Verify 'nonce' value isn't added to script and link HTML elements of the page.") {
			Navigator.openSpecificURL(url = "http://www.able.com:8080");

			var pageSource = selenium.getHtmlSource();

			TestUtils.assertNotContain(
				actual = ${pageSource},
				expected = "script nonce");

			TestUtils.assertNotContain(
				actual = ${pageSource},
				expected = "link nonce");
		}
	}

	@description = "This is a use case for LPS-179901. TC-1. Verify if 'nonce' value is added to all script and link HTML elements of the site page."
	@priority = 5
	test VerifyNonceIsAddedWhenCSPIsEnabledAtSiteLevel {
		property test.name.skip.portal.instance = "ContentSecurityPolicy#VerifyNonceIsAddedWhenCSPIsEnabledAtSiteLevel";

		task ("Given Add new site and add a new virtual host as SiteURL") {
			HeadlessSite.addSite(siteName = "Site Name");

			JSONLayout.addPublicLayout(
				groupName = "Site Name",
				layoutName = "Test Page");

			Site.openSiteSettingsAdmin(siteURLKey = "site-name");

			Site.addVirtualHostsURLCP(
				pageVirtualHosts = "true",
				pageVirtualHostURL = "www.able.com");

			UserBar.signOut();

			User.firstLoginUI(
				password = "test",
				specificURL = "http://www.able.com:8080",
				userEmailAddress = "test@liferay.com");
		}

		task ("And: Enable CSP at Site level.") {
			ContentSecurityPolicy.goToContentSecurityPolicySiteSettings(
				baseURL = "http://www.able.com:8080",
				siteURLKey = "site-name");

			ContentSecurityPolicy.configureCSP(
				csp = "img-src http://www.able.com",
				enableCSP = "true");
		}

		task ("When: Navigate to default site page and check HTML page source.") {
			Navigator.openSpecificURL(url = "http://www.able.com:8080");
		}

		task ("Then: Verify 'nonce' value is added to script and link HTML elements of the page.") {
			var pageSource = selenium.getHtmlSource();

			TestUtils.assertPartialEquals(
				actual = ${pageSource},
				expected = "script nonce");

			TestUtils.assertPartialEquals(
				actual = ${pageSource},
				expected = "link nonce");
		}

		task ("When: Add '/web/guest' as Excluded URI Path.") {
			ContentSecurityPolicy.goToContentSecurityPolicySiteSettings(
				baseURL = "http://www.able.com:8080",
				siteURLKey = "site-name");

			ContentSecurityPolicy.configureCSP(excludedPaths = "/c/portal/layout");
		}

		task ("Then: Verify 'nonce' value isn't added to script and link HTML elements of the page.") {
			Navigator.openSpecificURL(url = "http://www.able.com:8080");

			var pageSource = selenium.getHtmlSource();

			TestUtils.assertNotContain(
				actual = ${pageSource},
				expected = "script nonce");

			TestUtils.assertNotContain(
				actual = ${pageSource},
				expected = "link nonce");
		}
	}

}